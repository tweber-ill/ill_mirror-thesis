#!/usr/local/bin/gnuplot -p

#
# plots the voronoi performance comparison
# @author Tobias Weber <tweber@ill.fr>
# @date september-2021
# @license see 'LICENSE' file
#


# output format: 1: pdf, 2: png, others: screen
out_term = 1

if(out_term == 1) {
	set term pdf color enhanced font "DejaVu Sans, 45" size 11, 8
}
if(out_term == 2) {
	set term pngcairo enhanced font "DejaVu Sans, 32" size 1100, 800
}


# output files
if(out_term == 1) {
	file_out = "voronoi_performance.pdf"
	set output file_out
}
if(out_term == 2) {
	file_out = "voronoi_performance.png"
	set output file_out
}


# ranges
set xrange [ 0 : 300 ]
set yrange [ 0 : 30  ]


# ticks
set xtics  100
set mxtics 10

set ytics  10
set mytics 10


# colours
col_boost_gcc   = "#0000ff"
col_boost_clang = "#0000ff"
col_cgal_gcc    = "#ff0000"
col_cgal_clang  = "#ff0000"


# labels
set xlabel "Number of line segments" offset 0, 0.25
set ylabel "Time to calculate (s)" offset 1


# legend
set key top left samplen 1.5 Left reverse box opaque


set clip two
weight = 8
plot \
	"figures_svg/voronoi_performance_clang.dat" using ($1):($2) with lines lw weight lc rgb col_boost_clang title "Boost, Clang", \
	"figures_svg/voronoi_performance_clang.dat" using ($1):($3) with lines lw weight lc rgb col_cgal_clang title "CGAL, Clang", \
	"figures_svg/voronoi_performance_gcc.dat" using ($1):($2) with lines dt 2 lw weight lc rgb col_boost_gcc title "Boost, GCC", \
	"figures_svg/voronoi_performance_gcc.dat" using ($1):($3) with lines dt 2 lw weight lc rgb col_cgal_gcc title "CGAL, GCC", \
